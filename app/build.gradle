apply plugin: 'com.android.application'
apply plugin: 'jacoco'
if (!rootProject.plugins.hasPlugin("org.sonarqube")) {
	println("sonarcube not yet applied")
	apply plugin: 'org.sonarqube'
}

// add signing config
apply from: '../signing_configs.gradle'

apply from: '../versioning.gradle'

apply from: '../coverage.gradle'

android {
	compileSdkVersion project.ext.globalCompileSDK
    defaultConfig {
		manifestPlaceholders = [ appName:"SunnyEats" ]

        applicationId "hu.tvarga.sunnyeats"
		minSdkVersion project.ext.globalMinSDK
		targetSdkVersion project.ext.globalTargetSDK
		versionCode readVersionCode(project)
		versionName readVersionName(project)

		multiDexEnabled true

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

	compileOptions {
		targetCompatibility JavaVersion.VERSION_1_8
		sourceCompatibility JavaVersion.VERSION_1_8
	}

	buildTypes {
		debug {
			testCoverageEnabled true
		}
		release {
			minifyEnabled false
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
		}
	}
}

// default config values (prod)
apply from: '../default_config.gradle'

// add signing config
apply from: '../signing_configs.gradle'

// add build types
apply from: '../build_types.gradle'

apply from: '../common_dependencies.gradle'

dependencies {
	implementation project(":weather")
	implementation project(":restaurants")
	implementation project(":common")

	implementation "pub.devrel:easypermissions:1.1.3"
	implementation 'com.google.android.gms:play-services-location:15.0.1'

	implementation 'com.google.maps:google-maps-services:0.2.9'
	implementation 'org.slf4j:slf4j-nop:1.7.25'
}

def getAndroidTestCoverageFiles() {
	FileCollection androidTestCoverageFiles = fileTree("$buildDir/outputs/code-coverage/connected") {
		include "**/*.ec"
	}
	return androidTestCoverageFiles
}

sonarqube {
	properties {
		property "sonar.jacoco.itReportPath", getAndroidTestCoverageFiles()
		// NOTE: By default the java source set and not the android source set is used
		property "sonar.sources", android.sourceSets.main.java.srcDirs

		property "sonar.tests", "src/test/java"
		property "sonar.java.binaries", "${buildDir}/intermediates/classes/debug"
		property "sonar.java.test.binaries", "${buildDir}/intermediates/classes/test/debug"
		property "sonar.jacoco.reportPaths", "${buildDir}/jacoco/testDebugUnitTest.exec"
		property "sonar.junit.reportsPath", "${buildDir}/test-results/testDebugUnitTest"

		// sonar lint plugin
		//
		// ATTENTION: this file must match the chosen build flavor in the
		// CI build configurations, otherwise lint issues will not be visible in the sonar
		// report. CI Build Configs:
		//  - Android CI -> Sonar preview report generation
		//  - Android Master Sonar ->  SonarQube
		property "sonar.android.lint.report", "${buildDir}/reports/lint-results-debug.xml"
	}
}